BACKUP ~fl#valid_tests/backup~
AUTHOR Wisp

BEGIN ~Test suite for the fl#valid structure-validity library~

INCLUDE ~%MOD_FOLDER%/fl#valid.tpa~

/*
 * fl#valid configuration options
 */
OUTER_SET fl#valid#silent = 0

/*
 * Test suite configuration options
 */
/* Add a failing test to all test groups */
OUTER_SET fl#valid_tests#poison_tests = 0

/*
 * Test suite global state
 */
OUTER_SET success = 0
OUTER_SET fail = 0
OUTER_SPRINT failed_validity_tests ""
OUTER_SPRINT failed_invalidity_tests ""

DEFINE_ACTION_FUNCTION test_valid
  STR_VAR
    file = ""
    desc = ""
  RET
    success
    fail
    failed_validity_tests
BEGIN
  PRINT "Running validity test: %desc%"
  ACTION_TRY
    COPY - "%file%" ".../fl#valid_tests/%file%"
      LPF fl#valid STR_VAR file RET valid END
    ACTION_IF valid BEGIN
      OUTER_SET ++success
    END ELSE BEGIN
      OUTER_SET ++fail
      OUTER_SPRINT failed_validity_tests "%failed_validity_tests%%desc%%LNL%"
    END
  WITH
    DEFAULT
      OUTER_SET ++fail
      OUTER_SPRINT failed_validity_tests "%failed_validity_tests%%desc%%LNL%"
      WARN "Error caught: %ERROR_MESSAGE%"
  END
END

DEFINE_ACTION_FUNCTION test_invalid
  STR_VAR
    file = ""
    fail_mode = ""
    desc = ""
  RET
    success
    fail
    failed_invalidity_tests
BEGIN
  PRINT "Running invalidity test: %desc%"
  ACTION_TRY
    COPY - "%file%" ".../fl#valid_tests/%file%"
      LPF fl#valid STR_VAR file RET valid failure END
    ACTION_IF !valid AND "%failure%" STR_EQ "%fail_mode%" BEGIN
      OUTER_SET ++success
    END ELSE ACTION_IF !valid AND "%failure%" STR_CMP "%fail_mode%" BEGIN
      OUTER_SET ++fail
      OUTER_SPRINT failed_invalidity_tests "%failed_invalidity_tests%%desc%%LNL%"
      WARN "Framework error: check %fail_mode% should have failed, but test failed on %failure%"
    END ELSE BEGIN
      OUTER_SET ++fail
      OUTER_SPRINT failed_invalidity_tests "%failed_invalidity_tests%%desc%%LNL%"
    END
  WITH
    DEFAULT
      OUTER_SET ++fail
      OUTER_SPRINT failed_invalidity_tests "%failed_invalidity_tests%%desc%%LNL%"
      WARN "Error caught: %ERROR_MESSAGE%"
  END
END

ACTION_IF fl#valid_tests#poison_tests BEGIN
  PRINT "Adding poisoned tests"
  INCLUDE ~%MOD_FOLDER%/tests/poison.tpa~
END

PRINT "Testing known good structures"
INCLUDE ~%MOD_FOLDER%/tests/valid.tpa~

PRINT "Testing known bad structures"
INCLUDE ~%MOD_FOLDER%/tests/invalid.tpa~

PRINT "Testing concluded%LNL%Successful tests: %success%%LNL%Failed tests: %fail%"
ACTION_IF "%failed_validity_tests%" STR_CMP "" BEGIN
  WARN "Failed validity tests:%LNL%%failed_validity_tests%"
END
ACTION_IF "%failed_invalidity_tests%" STR_CMP "" BEGIN
  WARN "Failed invalidity tests:%LNL%%failed_invalidity_tests%"
END
